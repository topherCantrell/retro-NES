
; RESET vector points here. This code should identical and at the same origin
; in all banks of the ROM.
RESET:
    sei                        ; Disable interrupts
    cld                        ; Clear decimal mode (no hardware support)
    lda    #0                  ; Disable ...
    sta    PPUCTRL             ; ... NMI source
    sta    PPUMASK             ; Disable rendering
    sta    APU_DM_CONTROL      ; Diable DMC IRQ
    lda    #0x40               ; Disable ...
    sta    APU_JOYPAD2         ; ... APU frame IRQ  
    ldx    #0xFF               ; Top of ...
    txs                        ; ... stack

    lda    0x8000              ; Experiment ...
    sta    0x80                ; ... to see ...
    lda    0xC000              ; ... how banks are mapped ... 
    sta    0x81                ; ... at reset

; Wait for 2 vblanks to give NES hardware time to start
    lda    PPUSTATUS           ; Clear vblank flag
_wait1:
    lda    PPUSTATUS           ; Wait for ...
    and    #0x80               ; ... vblank ...
    beq    _wait1              ; ... flag
    lda    PPUSTATUS           ; Clear vblank flag
_wait2:
    lda    PPUSTATUS           ; Wait for ...
    and    #0x80               ; ... vblank ...
    beq    _wait2              ; ... flag

; RESET the MMC1 and configure it
    jsr     MMC1_reset          ; Reset the MMC1 mapper
    lda     #0x0F               ; Set MMC1 control to 8K CHR ROM, fixed/bank 16K PRG pages, ...
    jsr     MMC1_set_control    ; ... and horizontal mirroring (vertical scrolling)
    lda     #0x00               ; Set VROM bank ...
    jsr     MMC1_set_chr_bank_0 ; ... to bank 0    
    lda     #0x00               ; Bank 0 ...
    jsr     MMC1_set_prg_bank   ; ... in the low ROM bank

    jmp     START               ; Start of code
